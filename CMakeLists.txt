cmake_minimum_required(VERSION 3.10.0)
project(verona-lang CXX)

include(cmake/clangformat.cmake)
include(cmake/enable-asserts.cmake)

option(VERONA_LLVM_LOCATION "Location of LLVM/MLIR installation")
option(ENABLE_ASSERTS "Enable asserts even in release builds" OFF)
option(RT_TESTS "Including unit tests for the runtime" OFF)
option(USE_SCHED_STATS "Track scheduler stats" OFF)
option(USE_ASAN "Use address sanitizer" OFF)
option(VERONA_CI_BUILD "Disable features not sensible for CI" OFF)
option(USE_SYSTEMATIC_TESTING "Enable systematic testing in the runtime" OFF)
option(VERONA_EXPENSIVE_SYSTEMATIC_TESTING "Increase the range of seeds covered by systematic testing" OFF)
option(USE_CRASH_LOGGING "Enable crash logging in the runtime" OFF)
if (NOT MSVC)
  option(CMAKE_EXPORT_COMPILE_COMMANDS "Export compilation commands" ON)
endif ()

IF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
  SET(CMAKE_INSTALL_PREFIX ${CMAKE_BINARY_DIR}/dist CACHE PATH "Default to installing inside build dir" FORCE)
ENDIF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)

set(DEFAULT_BUILD_TYPE "Release")
if (NOT CMAKE_BUILD_TYPE)
  message(STATUS "Setting build type to '${DEFAULT_BUILD_TYPE}' as none was specified.")
  set(CMAKE_BUILD_TYPE "${DEFAULT_BUILD_TYPE}" CACHE STRING "Choose the type of build." FORCE)
endif()

message(STATUS "Build Type for Verona ${CMAKE_BUILD_TYPE}")
set(CMAKE_CONFIGURATION_TYPES  Release Debug RelWithDebInfo)
message(STATUS "Build types ${CMAKE_CONFIGURATION_TYPES}")

if (VERONA_LLVM_LOCATION)
  message(STATUS "Using LLVM/MLIR in ${VERONA_LLVM_LOCATION}")
  set(MLIR_DIR ${VERONA_LLVM_LOCATION}/lib/cmake/mlir)
  find_package(MLIR REQUIRED CONFIG)
else ()
  include(ExternalProject)

  find_package(Git REQUIRED)

  execute_process(
    COMMAND ${GIT_EXECUTABLE} rev-parse --short=11 HEAD
    WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}/external/llvm-project"
    OUTPUT_VARIABLE LLVM_PACKAGE_GIT_VERSION)
  string(STRIP ${LLVM_PACKAGE_GIT_VERSION} LLVM_PACKAGE_GIT_VERSION)
  message (STATUS "Detected LLVM GIT commit: ${LLVM_PACKAGE_GIT_VERSION}")

  if (${CMAKE_BUILD_TYPE} STREQUAL RelWithDebInfo)
    # RelWithDebInfo should use release LLVM.
    set(LLVM_BUILD_TYPE Release)
  elseif (WIN32)
    set(LLVM_BUILD_TYPE ${CMAKE_BUILD_TYPE})
  else ()
    # Use release everywhere it works.
    set(LLVM_BUILD_TYPE Release)
  endif ()

  set (LLVM_BASE ${CMAKE_BINARY_DIR}/llvm/${LLVM_BUILD_TYPE}/${LLVM_PACKAGE_GIT_VERSION})

  # If LLVM isn't present, and the user runs download-llvm/build-llvm targets,
  # we need to re-run cmake to take into load MLIR and enable the right targets.
  #
  # We use an llvm-stamp file as configure dependency to automatically run cmake
  # when that happens. Both the download-llvm and build-llvm targets touch that
  # stamp file.
  file(MAKE_DIRECTORY ${LLVM_BASE})
  file(TOUCH ${LLVM_BASE}/llvm-stamp)
  set_property(DIRECTORY APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS ${LLVM_BASE}/llvm-stamp)

  if (EXISTS "${LLVM_BASE}/install/lib/cmake/mlir/AddMLIR.cmake")
    message(STATUS "Using LLVM/MLIR in ${LLVM_BASE}/install/")
    set(MLIR_DIR ${LLVM_BASE}/install/lib/cmake/mlir)
    find_package(MLIR REQUIRED CONFIG)
  else ()
    message(WARNING "Could not find an LLVM installation. The MLIR backend will not be compiled.\nEither define VERONA_LLVM_LOCATION, use the `download-llvm` target to download a prebuilt LLVM blob or use the `build-llvm` to built it from source.")
    set (MLIR_FOUND FALSE)
  endif ()

  add_custom_target(download-llvm
    USES_TERMINAL # This allows us to print progress
    COMMAND
      ${CMAKE_COMMAND}
      -DCMAKE_SYSTEM_NAME=${CMAKE_SYSTEM_NAME}
      -DLLVM_BUILD_TYPE=${LLVM_BUILD_TYPE}
      -DLLVM_PACKAGE_GIT_VERSION=${LLVM_PACKAGE_GIT_VERSION}
      -DVERBOSE_LLVM_DOWNLOAD=${VERBOSE_LLVM_DOWNLOAD}
      -DOUTPUT_DIR=${LLVM_BASE}
      -P ${CMAKE_CURRENT_SOURCE_DIR}/cmake/download-llvm.cmake)

  ExternalProject_Add(build-llvm-internal
    PREFIX ${LLVM_BASE}
    BINARY_DIR ${LLVM_BASE}/build
    STAMP_DIR ${LLVM_BASE}/stamp
    SOURCE_DIR ${PROJECT_SOURCE_DIR}/external/llvm-project/llvm
    CMAKE_ARGS
      -DCMAKE_INSTALL_PREFIX=${LLVM_BASE}/install
      -DCMAKE_BUILD_TYPE=${LLVM_BUILD_TYPE}
      -DLLVM_ENABLE_PROJECTS=mlir
      -DLLVM_TARGETS_TO_BUILD=X86
      -DLLVM_ENABLE_ASSERTIONS=On
      -DLLVM_ENABLE_RTTI=ON
      -DLLVM_ENABLE_EH=ON
      ${LLVM_EXTRA_CMAKE_ARGS}
    USES_TERMINAL_BUILD true
    USES_TERMINAL_CONFIGURE true
    EXCLUDE_FROM_ALL true
    BUILD_ALWAYS true
  )

  add_custom_target(build-llvm
    COMMAND ${CMAKE_COMMAND} -E touch ${LLVM_BASE}/llvm-stamp)
  add_dependencies(build-llvm build-llvm-internal)
endif ()

if (VERONA_CI_BUILD)
  # Specify policy to go into child projects.
  set (CMAKE_POLICY_DEFAULT_CMP0077 NEW)
  set (SNMALLOC_CI_BUILD ON)
endif ()

clangformat_targets()

if (ENABLE_ASSERTS)
  enable_asserts()
endif()

if (MSVC)
  add_compile_options(/permissive-)
  add_compile_options(/utf-8)
  add_compile_options(/wd4307)
endif()

set(CMAKE_CXX_STANDARD 17)

add_subdirectory(external/CLI11 EXCLUDE_FROM_ALL)
add_subdirectory(external/fmt EXCLUDE_FROM_ALL)
add_subdirectory(external/pegmatite EXCLUDE_FROM_ALL)

add_library(cpp-peglib INTERFACE)
target_include_directories(cpp-peglib INTERFACE external/cpp-peglib)

add_subdirectory(src)
